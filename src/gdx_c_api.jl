# Copyright (c) 2026 Martin Kirk Bonde, James Daniel Foster and contributors
# Copyright (c) 2020-2023 GAMS Software GmbH
#
# Use of this source code is governed by an MIT-style license that can be found
# in the LICENSE.md file or at https://opensource.org/licenses/MIT.

# GDX library bindings (for libgdx)
# Low-level C API wrappers for reading and writing GDX files

# libgdx C library prefix for ccall:
const GDX_C_PREFIX = "c__"
macro cpfx(x)
    s = strip(string(x), ':')
    return Expr(:quote, Symbol(GDX_C_PREFIX, s))
end

# =============================================================================
# Constants
# =============================================================================

const GMS_MAX_INDEX_DIM = 20
const GMS_SSSIZE = 256
const GMS_VAL_MAX = 5

# GAMS value position in GDX data
const GAMS_VALUE_LEVEL = 1
const GAMS_VALUE_MARGINAL = 2
const GAMS_VALUE_LOWER = 3
const GAMS_VALUE_UPPER = 4
const GAMS_VALUE_SCALE = 5

# GAMS data types
const GMS_DT_SET = 0
const GMS_DT_PAR = 1
const GMS_DT_VAR = 2
const GMS_DT_EQU = 3
const GMS_DT_ALIAS = 4
const GMS_DT_MAX = 5

# GAMS special values
const GAMS_SV_UNDEF = 1.0e300       # undefined
const GAMS_SV_NA = 2.0e300       # not available / applicable
const GAMS_SV_PINF = 3.0e300       # plus infinity
const GAMS_SV_MINF = 4.0e300       # minus infinity
const GAMS_SV_EPS = 5.0e300       # epsilon
const GAMS_SV_ACR = 10.0e300      # potential / real acronym
const GAMS_SV_NAINT = 2100000000    # not available / applicable for integers

function parse_gdx_value(val::Float64)
    if val == GAMS_SV_UNDEF || val == GAMS_SV_NA
        return NaN
    end
    if val == GAMS_SV_PINF
        return Inf
    end
    if val == GAMS_SV_MINF
        return -Inf
    end
    if val == GAMS_SV_EPS
        return -0.0
    end
    return val
end

# =============================================================================
# GDX Handle
# =============================================================================

mutable struct GDXHandle
    cptr::Ref{Ptr{Cvoid}}
    cival::Ref{Cint}
    cival2::Ref{Cint}
    cival3::Ref{Cint}
    civec::Vector{Cint}
    crvec::Vector{Cdouble}
    buf::Vector{Vector{UInt8}}
    cbuf::Vector{Ptr{UInt8}}

    function GDXHandle()
        cptr = Ref{Ptr{Cvoid}}(C_NULL)
        buf = Vector{Vector{UInt8}}(undef, GMS_MAX_INDEX_DIM)
        for i in 1:GMS_MAX_INDEX_DIM
            buf[i] = Vector{UInt8}(undef, GMS_SSSIZE)
            buf[i] .= UInt8(' ')
        end
        cbuf = pointer.(buf)
        crvec = Vector{Cdouble}(undef, GMS_VAL_MAX)
        civec = Vector{Cint}(undef, GMS_MAX_INDEX_DIM)
        return new(
            cptr,
            Ref{Cint}(-1),
            Ref{Cint}(-1),
            Ref{Cint}(-1),
            civec,
            crvec,
            buf,
            cbuf,
        )
    end
end

struct GDXException <: Exception
    msg::String
    n_err::Int
end

function Base.showerror(io::IO, e::GDXException)
    return print(io, "GDX failed: $(e.msg) ($(e.n_err))")
end

# =============================================================================
# Core handle management
# =============================================================================

function gdx_create(gdx_ptr::Ref{Ptr{Cvoid}})
    ccall((:xcreate, libgdx), Cvoid, (Ptr{Ptr{Cvoid}},), gdx_ptr)
    if gdx_ptr[] == C_NULL
        throw(GDXException("Can't create GAMS GDX object", 0))
    end
    return
end

gdx_create(gdx::GDXHandle) = gdx_create(gdx.cptr)

function gdx_free(gdx_ptr::Ref{Ptr{Cvoid}})
    ccall((:xfree, libgdx), Cvoid, (Ptr{Ptr{Cvoid}},), gdx_ptr)
    return
end

gdx_free(gdx::GDXHandle) = gdx_free(gdx.cptr)

# =============================================================================
# File operations
# =============================================================================

function gdx_open_read(gdx_ptr::Ptr{Cvoid}, file::String, n_err::Ref{Cint})
    return ccall(
        (@cpfx(:gdxopenread), libgdx),
        Cint,
        (Ptr{Cvoid}, Cstring, Ref{Cint}),
        gdx_ptr,
        file,
        n_err,
    )
end

function gdx_open_read(gdx::GDXHandle, file::String)
    rc = gdx_open_read(gdx.cptr[], file, gdx.cival)
    if gdx.cival[] != 0 || rc != 1
        throw(GDXException("Can't open file '$file'", gdx.cival[]))
    end
    return rc
end

function gdx_open_write(
    gdx_ptr::Ptr{Cvoid},
    file::String,
    producer::String,
    n_err::Ref{Cint},
)
    return ccall(
        (@cpfx(:gdxopenwrite), libgdx),
        Cint,
        (Ptr{Cvoid}, Cstring, Cstring, Ref{Cint}),
        gdx_ptr,
        file,
        producer,
        n_err,
    )
end

function gdx_open_write(
    gdx::GDXHandle,
    file::String,
    producer::String = "GAMS.jl",
)
    rc = gdx_open_write(gdx.cptr[], file, producer, gdx.cival)
    if gdx.cival[] != 0 || rc != 1
        throw(GDXException("Can't open file '$file' for writing", gdx.cival[]))
    end
    return
end

function gdx_close(gdx_ptr::Ptr{Cvoid})
    return ccall((@cpfx(:gdxclose), libgdx), Cint, (Ptr{Cvoid},), gdx_ptr)
end

function gdx_close(gdx::GDXHandle)
    gdx_close(gdx.cptr[])
    return
end

# =============================================================================
# System and symbol information
# =============================================================================

function gdx_system_info(
    gdx_ptr::Ptr{Cvoid},
    sym_count::Ref{Cint},
    uel_count::Ref{Cint},
)
    return ccall(
        (@cpfx(:gdxsysteminfo), libgdx),
        Cint,
        (Ptr{Cvoid}, Ref{Cint}, Ref{Cint}),
        gdx_ptr,
        sym_count,
        uel_count,
    )
end

function gdx_system_info(gdx::GDXHandle)
    rc = gdx_system_info(gdx.cptr[], gdx.cival, gdx.cival2)
    if rc != 1
        throw(GDXException("Can't read system info", 0))
    end
    return Int(gdx.cival[]), Int(gdx.cival2[])
end

function gdx_symbol_info(
    gdx_ptr::Ptr{Cvoid},
    sym_id::Int,
    name::Ptr{UInt8},
    dim::Ref{Cint},
    type::Ref{Cint},
)
    return ccall(
        (@cpfx(:gdxsymbolinfo), libgdx),
        Cint,
        (Ptr{Cvoid}, Cint, Ptr{UInt8}, Ref{Cint}, Ref{Cint}),
        gdx_ptr,
        sym_id,
        name,
        dim,
        type,
    )
end

function gdx_symbol_info(gdx::GDXHandle, sym_id::Int)
    gdx.buf[1][1] = UInt8('\0')
    rc = gdx_symbol_info(gdx.cptr[], sym_id, gdx.cbuf[1], gdx.cival, gdx.cival2)
    if rc != 1
        throw(GDXException("Can't read symbol info", 0))
    end
    return unsafe_string(gdx.cbuf[1]), Int(gdx.cival[]), Int(gdx.cival2[])
end

function gdx_symbol_info_x(
    gdx_ptr::Ptr{Cvoid},
    sym_id::Int,
    count::Ref{Cint},
    user_info::Ref{Cint},
    text::Ptr{UInt8},
)
    return ccall(
        (@cpfx(:gdxsymbolinfox), libgdx),
        Cint,
        (Ptr{Cvoid}, Cint, Ref{Cint}, Ref{Cint}, Ptr{UInt8}),
        gdx_ptr,
        sym_id,
        count,
        user_info,
        text,
    )
end

function gdx_symbol_info_x(gdx::GDXHandle, sym_id::Int)
    gdx.buf[1][1] = UInt8('\0')
    rc = gdx_symbol_info_x(
        gdx.cptr[],
        sym_id,
        gdx.cival,
        gdx.cival2,
        gdx.cbuf[1],
    )
    if rc != 1
        throw(GDXException("Can't read extended symbol info", 0))
    end
    return Int(gdx.cival[]), Int(gdx.cival2[]), unsafe_string(gdx.cbuf[1])
end

function gdx_find_symbol(gdx_ptr::Ptr{Cvoid}, name::String, sym_nr::Ref{Cint})
    return ccall(
        (@cpfx(:gdxfindsymbol), libgdx),
        Cint,
        (Ptr{Cvoid}, Cstring, Ref{Cint}),
        gdx_ptr,
        name,
        sym_nr,
    )
end

function gdx_find_symbol(gdx::GDXHandle, name::String)
    rc = gdx_find_symbol(gdx.cptr[], name, gdx.cival)
    return rc == 1, Int(gdx.cival[])
end

function gdx_symbol_get_domain_x(
    gdx_ptr::Ptr{Cvoid},
    sym_nr::Int,
    domains::Vector{Ptr{UInt8}},
)
    return ccall(
        (@cpfx(:gdxsymbolgetdomainx), libgdx),
        Cint,
        (Ptr{Cvoid}, Cint, Ptr{Ptr{UInt8}}),
        gdx_ptr,
        sym_nr,
        domains,
    )
end

function gdx_symbol_get_domain_x(gdx::GDXHandle, sym_nr::Int, dim::Int)
    for i in 1:dim
        gdx.buf[i][1] = UInt8('\0')
    end
    rc = gdx_symbol_get_domain_x(gdx.cptr[], sym_nr, gdx.cbuf)
    domains = Vector{String}(undef, dim)
    for i in 1:dim
        domains[i] = unsafe_string(gdx.cbuf[i])
    end
    return domains
end

function gdx_symbol_set_domain_x(
    gdx_ptr::Ptr{Cvoid},
    sym_nr::Int,
    domain_ids::Vector{String},
)
    return ccall(
        (@cpfx(:gdxsymbolsetdomainx), libgdx),
        Cint,
        (Ptr{Cvoid}, Cint, Ptr{Cstring}),
        gdx_ptr,
        sym_nr,
        domain_ids,
    )
end

function gdx_symbol_set_domain_x(
    gdx::GDXHandle,
    sym_nr::Int,
    domain_ids::Vector{String},
)
    rc = gdx_symbol_set_domain_x(gdx.cptr[], sym_nr, domain_ids)
    return rc
end

# =============================================================================
# Alias
# =============================================================================

function gdx_add_alias(gdx_ptr::Ptr{Cvoid}, id1::String, id2::String)
    return ccall(
        (@cpfx(:gdxaddalias), libgdx),
        Cint,
        (Ptr{Cvoid}, Cstring, Cstring),
        gdx_ptr,
        id1,
        id2,
    )
end

function gdx_add_alias(gdx::GDXHandle, id1::String, id2::String)
    rc = gdx_add_alias(gdx.cptr[], id1, id2)
    if rc != 1
        throw(GDXException("Can't add alias '$id2' for '$id1'", 0))
    end
    return
end

# =============================================================================
# Set element text
# =============================================================================

function gdx_get_elem_text(
    gdx_ptr::Ptr{Cvoid},
    text_nr::Int,
    text::Ptr{UInt8},
    node::Ref{Cint},
)
    return ccall(
        (@cpfx(:gdxgetelemtext), libgdx),
        Cint,
        (Ptr{Cvoid}, Cint, Ptr{UInt8}, Ref{Cint}),
        gdx_ptr,
        text_nr,
        text,
        node,
    )
end

function gdx_get_elem_text(gdx::GDXHandle, text_nr::Int)
    gdx.buf[1][1] = UInt8('\0')
    rc = gdx_get_elem_text(gdx.cptr[], text_nr, gdx.cbuf[1], gdx.cival)
    return rc == 1, unsafe_string(gdx.cbuf[1])
end

function gdx_add_set_text(gdx_ptr::Ptr{Cvoid}, text::String, text_nr::Ref{Cint})
    return ccall(
        (@cpfx(:gdxaddsettext), libgdx),
        Cint,
        (Ptr{Cvoid}, Cstring, Ref{Cint}),
        gdx_ptr,
        text,
        text_nr,
    )
end

function gdx_add_set_text(gdx::GDXHandle, text::String)
    rc = gdx_add_set_text(gdx.cptr[], text, gdx.cival)
    if rc != 1
        throw(GDXException("Can't register set element text", 0))
    end
    return Int(gdx.cival[])
end

# =============================================================================
# UEL (Unique Element List) operations
# =============================================================================

function gdx_um_uel_get(
    gdx_ptr::Ptr{Cvoid},
    uel_nr::Int,
    uel::Ptr{UInt8},
    uel_map::Ref{Cint},
)
    return ccall(
        (@cpfx(:gdxumuelget), libgdx),
        Cint,
        (Ptr{Cvoid}, Cint, Ptr{UInt8}, Ref{Cint}),
        gdx_ptr,
        uel_nr,
        uel,
        uel_map,
    )
end

function gdx_um_uel_get(gdx::GDXHandle, uel_nr::Int)
    gdx.buf[1][1] = UInt8('\0')
    rc = gdx_um_uel_get(gdx.cptr[], uel_nr, gdx.cbuf[1], gdx.cival)
    if rc != 1
        throw(GDXException("Can't get UEL #$uel_nr", 0))
    end
    return unsafe_string(gdx.cbuf[1])
end

function gdx_uel_register_str_start(gdx_ptr::Ptr{Cvoid})
    return ccall(
        (@cpfx(:gdxuelregisterstrstart), libgdx),
        Cint,
        (Ptr{Cvoid},),
        gdx_ptr,
    )
end

function gdx_uel_register_str_start(gdx::GDXHandle)
    rc = gdx_uel_register_str_start(gdx.cptr[])
    if rc != 1
        throw(GDXException("Can't start UEL string registration", 0))
    end
    return
end

function gdx_uel_register_str(
    gdx_ptr::Ptr{Cvoid},
    uel::String,
    uel_nr::Ref{Cint},
)
    return ccall(
        (@cpfx(:gdxuelregisterstr), libgdx),
        Cint,
        (Ptr{Cvoid}, Cstring, Ref{Cint}),
        gdx_ptr,
        uel,
        uel_nr,
    )
end

function gdx_uel_register_str(gdx::GDXHandle, uel::String)
    rc = gdx_uel_register_str(gdx.cptr[], uel, gdx.cival)
    return Int(gdx.cival[])
end

function gdx_uel_register_done(gdx_ptr::Ptr{Cvoid})
    return ccall(
        (@cpfx(:gdxuelregisterdone), libgdx),
        Cint,
        (Ptr{Cvoid},),
        gdx_ptr,
    )
end

function gdx_uel_register_done(gdx::GDXHandle)
    rc = gdx_uel_register_done(gdx.cptr[])
    if rc != 1
        throw(GDXException("Can't finish UEL registration", 0))
    end
    return
end

# =============================================================================
# Reading data (raw integer interface)
# =============================================================================

function gdx_data_read_raw_start(
    gdx_ptr::Ptr{Cvoid},
    start::Int,
    n_rec::Ref{Cint},
)
    return ccall(
        (@cpfx(:gdxdatareadrawstart), libgdx),
        Cint,
        (Ptr{Cvoid}, Cint, Ref{Cint}),
        gdx_ptr,
        start,
        n_rec,
    )
end

function gdx_data_read_raw_start(gdx::GDXHandle, start::Int)
    rc = gdx_data_read_raw_start(gdx.cptr[], start, gdx.cival)
    if rc != 1
        throw(GDXException("Can't start GDX read", 0))
    end
    return Int(gdx.cival[])
end

function gdx_data_read_raw(
    gdx_ptr::Ptr{Cvoid},
    idx::Vector{Cint},
    vals::Vector{Cdouble},
    dim::Ref{Cint},
)
    return ccall(
        (@cpfx(:gdxdatareadraw), libgdx),
        Cint,
        (Ptr{Cvoid}, Ptr{Cint}, Ptr{Cdouble}, Ref{Cint}),
        gdx_ptr,
        idx,
        vals,
        dim,
    )
end

function gdx_data_read_raw(
    gdx::GDXHandle,
    idx::Vector{Int},
    vals::Vector{Float64},
)
    @assert(length(idx) <= length(gdx.civec))
    @assert(length(vals) <= length(gdx.crvec))

    rc = gdx_data_read_raw(gdx.cptr[], gdx.civec, gdx.crvec, gdx.cival)
    if rc != 1
        throw(GDXException("Reading raw data failed", 0))
    end

    for i in 1:length(idx)
        idx[i] = gdx.civec[i]
    end
    for i in 1:length(vals)
        vals[i] = gdx.crvec[i]
    end
    return
end

# =============================================================================
# Reading data (string interface)
# =============================================================================

function gdx_data_read_str_start(
    gdx_ptr::Ptr{Cvoid},
    start::Int,
    n_err::Ref{Cint},
)
    return ccall(
        (@cpfx(:gdxdatareadstrstart), libgdx),
        Cint,
        (Ptr{Cvoid}, Cint, Ref{Cint}),
        gdx_ptr,
        start,
        n_err,
    )
end

function gdx_data_read_str_start(gdx::GDXHandle, start::Int)
    rc = gdx_data_read_str_start(gdx.cptr[], start, gdx.cival)
    if rc != 1
        throw(GDXException("Can't start GDX read", 0))
    end
    return Int(gdx.cival[])
end

function gdx_data_read_str(
    gdx_ptr::Ptr{Cvoid},
    keystr::Vector{Ptr{UInt8}},
    vals::Vector{Cdouble},
    dim_first::Ref{Cint},
)
    return ccall(
        (@cpfx(:gdxdatareadstr), libgdx),
        Cint,
        (Ptr{Cvoid}, Ptr{Ptr{UInt8}}, Ptr{Cdouble}, Ref{Cint}),
        gdx_ptr,
        keystr,
        vals,
        dim_first,
    )
end

function gdx_data_read_str(
    gdx::GDXHandle,
    keystr::Vector{String},
    vals::Vector{Float64},
)
    @assert(length(keystr) <= length(gdx.cbuf))
    @assert(length(vals) <= length(gdx.crvec))

    for b in gdx.buf
        b[1] = UInt8('\0')
    end

    rc = gdx_data_read_str(gdx.cptr[], gdx.cbuf, gdx.crvec, gdx.cival)
    if rc != 1
        throw(GDXException("Failed to read GDX record", 0))
    end

    for i in 1:length(keystr)
        keystr[i] = unsafe_string(gdx.cbuf[i])
    end
    for i in 1:length(vals)
        vals[i] = gdx.crvec[i]
    end
    return
end

function gdx_data_read_done(gdx_ptr::Ptr{Cvoid})
    return ccall(
        (@cpfx(:gdxdatareaddone), libgdx),
        Cint,
        (Ptr{Cvoid},),
        gdx_ptr,
    )
end

function gdx_data_read_done(gdx::GDXHandle)
    gdx_data_read_done(gdx.cptr[])
    return
end

# =============================================================================
# Writing data
# =============================================================================

function gdx_data_write_str_start(
    gdx_ptr::Ptr{Cvoid},
    name::String,
    text::String,
    dim::Int,
    typ::Int,
    user_info::Int,
)
    return ccall(
        (@cpfx(:gdxdatawritestrstart), libgdx),
        Cint,
        (Ptr{Cvoid}, Cstring, Cstring, Cint, Cint, Cint),
        gdx_ptr,
        name,
        text,
        dim,
        typ,
        user_info,
    )
end

function gdx_data_write_str_start(
    gdx::GDXHandle,
    name::String,
    text::String,
    dim::Int,
    typ::Int,
    user_info::Int = 0,
)
    rc = gdx_data_write_str_start(gdx.cptr[], name, text, dim, typ, user_info)
    if rc != 1
        throw(GDXException("Can't start writing symbol '$name'", 0))
    end
    return
end

function gdx_data_write_str(
    gdx_ptr::Ptr{Cvoid},
    keys::Vector{String},
    vals::Vector{Float64},
)
    return ccall(
        (@cpfx(:gdxdatawritestr), libgdx),
        Cint,
        (Ptr{Cvoid}, Ptr{Cstring}, Ptr{Cdouble}),
        gdx_ptr,
        keys,
        vals,
    )
end

function gdx_data_write_str(
    gdx::GDXHandle,
    keys::Vector{String},
    vals::Vector{Float64},
)
    rc = gdx_data_write_str(gdx.cptr[], keys, vals)
    if rc != 1
        throw(GDXException("Can't write record", 0))
    end
    return
end

function gdx_data_write_done(gdx_ptr::Ptr{Cvoid})
    return ccall(
        (@cpfx(:gdxdatawritedone), libgdx),
        Cint,
        (Ptr{Cvoid},),
        gdx_ptr,
    )
end

function gdx_data_write_done(gdx::GDXHandle)
    rc = gdx_data_write_done(gdx.cptr[])
    if rc != 1
        throw(GDXException("Can't finish writing symbol", 0))
    end
    return
end
